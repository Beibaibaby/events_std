#connection and input parameters
par jee=10, jei=2, jie=10, jii=1
par Ie=0.05,Ii=-0.2
#system size
par N=500
#excitatory and inhibitory timescales
par taue=1, taui=2
#plasticity parameters
par tree=20,tdee=5
par trie=20,tdie=5
par threshee=0.3, threshie=0.1
par bee=50, bie=50
par mee=4, mie=4

#sigmoidal transfer function
f(u)=1/(1+exp(-sqrt(N)*u))
#probability of release functions
ar(u,a,b,m)= m/(1+exp(-b*(u-a)))
pr(u,a,b,m,c,d)= d/(d+c*ar(u,a,b,m))

#governing equations
ve'  = (-ve+f(jee*pree*ve-jei*vi+Ie))/taue
vi'  = (-vi+f(jie*prie*ve-jii*vi+Ii))/taui
prie' = ((1-prie)/trie) - prie*ar(ve,threshie,bie,mie)/tdie 
pree' = ((1-pree)/tree) - pree*ar(ve,threshee,bee,mee)/tdee 

#auxiliary variables to track
aux rie = vi*prie
aux ree = ve*pree

#initial conditions
init ve=0.01, vi=0.01, pree=1, prie=1

#various visualization and numerical settings
@ xp1=ve,yp1=vi,xlo=-0.05,xhi=1.05,ylo=-0.05,yhi=1.05
@ nmesh=200,total=200,nplot=1
@ meth=cvode,atol=1e-10,tol=1e-10
done
